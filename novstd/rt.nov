import "std/console.nov"
import "std/path.nov"
import "std/time.nov"
import "std/version.nov"

// -- Types

enum Platform =
  Linux   : 1,
  MacOs   : 2,
  Windows : 3

// -- Conversions

fun string(Platform p)
  if p == Platform.Linux    -> "Linux"
  if p == Platform.MacOs    -> "MacOs"
  if p == Platform.Windows  -> "Windows"
  else                      -> "Unknown"

// -- Platform

act getPlatform() Platform(intrinsic{runtime_platform}())

// -- Versions

act getRuntimeVersion() -> Version
  versionParser().run(intrinsic{version_runtime}()).failOnError()

act getCompilerVersion() -> Version
  versionParser().run(intrinsic{version_compiler}()).failOnError()

// -- Paths

act getPathCurrent() -> PathAbsolute
  pathAbsParser().run(intrinsic{path_workingdirectory}()).failOnError()

act getPathRuntime() -> PathAbsolute
  pathAbsParser().run(intrinsic{path_runtime}()).failOnError()

act getPathProgram() -> PathAbsolute
  pathAbsParser().run(intrinsic{path_program}()).failOnError()

// -- Interupt

act interuptIsRequested() -> bool
  intrinsic{interupt_isrequested}()

act interuptReset() -> bool
  intrinsic{interupt_reset}()

act atInterupt{T}(action{T} delegate) -> future{T}
  fork invoke(impure lambda () -> T
    if interuptIsRequested() -> delegate()
    else                     -> sleep(milliseconds(100)); self())

// -- Misc

act sleep(Duration d)
  Duration(intrinsic{sleep_nano}(d.ns))

// -- Failure

act fail(string msg) -> Option{Error}
  fail{Option{Error}}(msg)

act fail{TResult}(string msg) -> TResult
  printErr(msg);
  failfast{TResult}()

// -- Tests

assert(
  p = getPlatform();
  p == Platform.Linux || p == Platform.MacOs || p == Platform.Windows)

assert(getRuntimeVersion() >= Version(0, 5, 0))

assert(getCompilerVersion() >= Version(0, 5, 0))

assert(getPathCurrent().extension() is None)

assert(
  runtimeName = getPathRuntime().stem();
  runtimeName == "novrt" || runtimeName == "nove")

assert(getPathProgram().filename() == "rt.nov")

assert(getPathProgram().stem() == "rt")

assert(getPathProgram().extension() == "nov")
