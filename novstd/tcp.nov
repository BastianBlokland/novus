import "std/future.nov"
import "std/ip.nov"
import "std/option.nov"
import "std/parallel.nov"

// -- Types

struct TcpConnection =
  sys_stream socket

struct TcpServerState =
  int connectionCounter

struct TcpServerSettings =
  IpFamily                                              family,
  int                                                   port,
  int                                                   maxBacklog,
  action{TcpConnection, TcpServerState, Option{Error}}  clientHandler,
  action{TcpServerState, bool}                          cancelPredicate

// -- Constructors

fun TcpServerSettings(int port, action{TcpConnection, TcpServerState, Option{Error}} clientHandler)
  TcpServerSettings(IpFamily.V4, port, clientHandler)

fun TcpServerSettings(IpFamily family, int port, action{TcpConnection, TcpServerState, Option{Error}} clientHandler)
  TcpServerSettings(family, port, 64, clientHandler)

fun TcpServerSettings(IpFamily family, int port, int maxBacklog, action{TcpConnection, TcpServerState, Option{Error}} clientHandler)
  TcpServerSettings(
    family,
    port,
    maxBacklog,
    clientHandler,
    impure lambda (TcpServerState state) interuptIsRequested())

// -- Connection

act write(TcpConnection c, string str) -> Option{Error}
  c.socket.write(str)
    ? None()
    : Error("Failed to write to the socket")

act write(TcpConnection c, char ch) -> Option{Error}
  c.socket.write(ch)
    ? None()
    : Error("Failed to write to the socket")

act write{T}(TcpConnection c, Writer{T} writer, T val) -> Option{Error}
  c.write(writer.run(val))

// -- Client

act tcpConnection(IpAddress addr) -> Either{TcpConnection, Error}
  tcpConnection(addr, 8080)

act tcpConnection(IpAddress addr, int port) -> Either{TcpConnection, Error}
  binAddr = ipBinWriter().run(addr);
  socket  = tcpOpenConnection(binAddr, IpFamily(addr).int(), port);
  if socket.streamCheckValid()  -> TcpConnection(socket)
  else                          -> Error("Failed to open connection to: '" + addr.string() + " [" + port + "]")

act tcpConnectionAsync(IpAddress addr) -> future{Either{TcpConnection, Error}}
  fork tcpConnection(addr)

act tcpConnectionAsync(IpAddress addr, int port) -> future{Either{TcpConnection, Error}}
  fork tcpConnection(addr, port)

// -- Server

act tcpServer(TcpServerSettings settings) -> Either{TcpServerState, Error}
  serverSocket = tcpStartServer(settings.family.int(), settings.port, settings.maxBacklog);
  if !serverSocket.streamCheckValid() -> Error("Failed to start tcp-server")
  else ->
    acceptCon = (impure lambda () -> Either{TcpConnection, Error}
      socket = tcpAcceptConnection(serverSocket);
      if socket.streamCheckValid()  -> TcpConnection(socket)
      else                          -> Error("Failed to accept new connection")
    );
    loop = (impure lambda (
      future{Either{TcpConnection, Error}} futureConnection,
      List{future{Option{Error}}}          connections,
      TcpServerState                       state)

        if futureConnection.get(milliseconds(100)) as Either{TcpConnection, Error} newConnection ->
        (
          if newConnection as Error          err  -> err
          if newConnection as TcpConnection  c    ->
            newState = TcpServerState(state.connectionCounter + 1);
            self(fork acceptCon(), fork settings.clientHandler(c, newState) :: connections, newState)
        )
        else ->
        (
          conErrOpt = connections.map(    impure lambda (future{Option{Error}} c) c.poll().unwrap()).combine();
          remCons   = connections.filter( impure lambda (future{Option{Error}} c) c.poll() is None);
          if conErrOpt as Error conErr        -> conErr
          if !settings.cancelPredicate(state) -> self(futureConnection, remCons, state)
          else                                -> state
        )
    );
    loop(fork acceptCon(), List{future{Option{Error}}}(), TcpServerState(0))

// -- Tests

assert(
  clientHandler = (impure lambda (TcpConnection c, TcpServerState state) Option{Error}());
  tcpServer(TcpServerSettings(IpFamily.V4, -1, 64, clientHandler))          is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V4, 65536, 64, clientHandler))       is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V4, intMax(), 64, clientHandler))    is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V4, 5000, intMax(), clientHandler))  is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V6, -1, 64, clientHandler))          is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V6, 65536, 64, clientHandler))       is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V6, intMax(), 64, clientHandler))    is Error  &&
  tcpServer(TcpServerSettings(IpFamily.V6, 5000, intMax(), clientHandler))  is Error
)

assert(
  clientHandler = (impure lambda (TcpConnection c, TcpServerState state)
    c.write(c.socket.readLine() + '\n')
  );
  fork tcpServer(TcpServerSettings(IpFamily.V4, 5001, clientHandler));
  client = (impure lambda(string message)
    c = tcpConnection(ipV4Loopback(), 5001).failOnError();
    c.write(message + '\n').failOnError();
    c.socket.readLine()
  );
  parallelFor(100, impure lambda (int i)
    msg = string("Hello from the network", i);
    client(msg) == msg
  ).all()
)

assert(
  clientHandler = (impure lambda (TcpConnection c, TcpServerState state)
    c.write(c.socket.readLine() + '\n')
  );
  fork tcpServer(TcpServerSettings(IpFamily.V6, 5002, clientHandler));
  client = (impure lambda(string message)
    c = tcpConnection(ipV6Loopback(), 5002).failOnError();
    c.write(message + '\n').failOnError();
    c.socket.readLine()
  );
  parallelFor(100, impure lambda (int i)
    msg = string("Hello from the network", i);
    client(msg) == msg
  ).all()
)

assert(
  clientHandler = (impure lambda (TcpConnection c, TcpServerState state)
    Option(Error("Did not go well"))
  );
  res = fork tcpServer(TcpServerSettings(IpFamily.V6, 5003, clientHandler));
  client = tcpConnection(ipV6Loopback(), 5003).failOnError();
  res.get() == Error("Did not go well")
)

assert(
  clientHandler = (impure lambda (TcpConnection c, TcpServerState state)
    c.write("hello")
  );
  cancelPredicate = (impure lambda (TcpServerState state)
    true
  );
  tcpServer(TcpServerSettings(IpFamily.V6, 5004, 64, clientHandler, cancelPredicate)) == TcpServerState(0)
)

assert(
  cancelPredicate = (impure lambda (TcpServerState state)
    state.connectionCounter == 100
  );
  clientHandler = (impure lambda (TcpConnection c, TcpServerState state)
    c.write("Hello\n")
  );
  server = fork tcpServer(TcpServerSettings(IpFamily.V6, 5005, 64, clientHandler, cancelPredicate));
  parallelFor(100, impure lambda (int i)
    tcpConnection(ipV6Loopback(), 5005)
  );
  server.get() == TcpServerState(100)
)
