import "either.nov"
import "error.nov"
import "list.nov"
import "option.nov"
import "parse.nov"
import "text.nov"

// Types

struct EnvOpt = string id, List{string} args

// Conversions

fun string(EnvOpt eo)
  if eo.args.isEmpty()  -> eo.id
  else                  -> eo.id + eo.args.string()

// Functions

fun findEnvOpt(List{string} args, string name) -> Option{EnvOpt}
  short  = name.length() == 1;
  long   = name.length() > 1;
  if args.front() as string s ->
    if short && s.isShortOpt() && s.contains(name)  -> EnvOpt(name, args.pop().takeNonOpts())
    if long && s == "--" + name                     -> EnvOpt(name, args.pop().takeNonOpts())
    else                                            -> findEnvOpt(args.pop(), name)
  else -> None()

fun isShortOpt(string s)
  s[0] == '-' && s[1] != '-'

fun isLongOpt(string s)
  s[0] == '-' && s[1] == '-'

fun takeNonOpts(List{string} l)
  l.take(lambda (string str) str[0] != '-' || str[1].isDigit())

fun findEnvOptVal{T}(List{string} args, string n, function{string, Option{T}} p) -> Either{T, Error}
  if findEnvOpt(args, n) as EnvOpt o  ->
    if o.args.front().map(p) as T t -> t
    if o.args.isEmpty()             -> Error(2, "Option has no arguments")
    else                            -> Error(3, "Failed to parse argument")
  else                                -> Error(1, "Option not found")

// Actions

act getIntEnvOpt(string name)
  findEnvOptVal(getEnvArgs(), name, parseInt)

act getFloatEnvOpt(string name)
  findEnvOptVal(getEnvArgs(), name, parseFloat)

act getEnvOpt(string name)
  findEnvOpt(getEnvArgs(), name)

act getEnvArgs()
  (
    impure lambda (int idx, List{string} result)
      if idx < 0  -> result
      else        -> self(--idx, getEnvArg(idx) :: result)
  )(--getEnvArgCount(), List{string}())

// Tests

assert(
  findEnvOpt("-f" :: List{string}(), "f") is EnvOpt &&
  findEnvOpt("-d" :: List{string}(), "f") is None &&
  findEnvOpt("-df" :: List{string}(), "d") is EnvOpt &&
  findEnvOpt("-df" :: List{string}(), "f") is EnvOpt &&
  findEnvOpt("a" :: "-b" :: "-df" :: "-c" :: List{string}(), "f") is EnvOpt &&
  findEnvOpt("-f" :: List{string}(), "") is None)

assert(
  args = "b" :: "-1" :: "c" :: "d" :: List{string}();
  in = "a" :: "-dfz" :: args :: "-t" :: List{string}();
  findEnvOpt(in, "f") as EnvOpt o && o.args == args)

assert(
  findEnvOpt("--file" :: List{string}(), "file") is EnvOpt &&
  findEnvOpt("--files" :: List{string}(), "file") is None &&
  findEnvOpt("--hello" ::  "a" :: "--file" :: "--world" :: List{string}(), "file") is EnvOpt)

assert(
  args = "b" :: "c" :: "d" :: List{string}();
  in = "a" :: "--file" :: args :: "-t" :: List{string}();
  findEnvOpt(in, "file") as EnvOpt o && o.args == args)

assert(
  findEnvOptVal("--test" :: "42" :: List{string}(), "test", parseInt) == 42 &&
  findEnvOptVal("--test" :: List{string}(), "test", parseInt) is Error &&
  findEnvOptVal("--test" :: "hello" :: List{string}(), "test", parseInt) is Error &&
  findEnvOptVal(List{string}(), "test", parseInt) is Error)
