// -- Types

enum StreamOptions =
  NoBlock : 0b1

// -- Actions

act setOptions(stream s, StreamOptions opts)
  s.streamSetOptions(int(opts))

act unsetOptions(stream s, StreamOptions opts)
  s.streamUnsetOptions(int(opts))

act write(stream s, string str)
  s.streamWrite(str)

act write(stream s, char c)
  s.streamWrite(c)

act read(stream s)
  (
    impure lambda (string result)
      read = s.streamRead(512);
      if read.length() > 0  -> self(result + read)
      else                  -> result
  )("")

act readChar(stream s)
  s.streamRead()

act readLine(stream s)
  (
    impure lambda (string result)
      c = s.streamRead();
      if c == '\r'              -> self(result)
      if c == '\n' || c == '\0' -> result
      else                      -> self(result + c)
  )("")

act copy(stream from, stream to)
  (
    impure lambda(int bytesCopied)
      read        = from.streamRead(512);
      readLength  = read.length();
      if readLength > 0 -> to.streamWrite(read); self(bytesCopied + readLength)
      else              -> bytesCopied
  )(0)
