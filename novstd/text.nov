import "list.nov"
import "func.nov"

// Operators

fun +{T}(string s, T v)
  s + v.string()

// Functions

fun isEmpty(string str)
  str.length() == 0

fun appendChar(string str, char c)
  str + c

fun contains(string str, string subStr)
  str.indexOf(subStr) >= 0

fun indexOf(string str, string subStr)
  indexOf(str, 0, subStr, 0)

fun indexOf(string str, int idx, string subStr, int subStrIdx)
  if idx >= str.length()            -> -1
  if str[idx] == subStr[subStrIdx]  -> subStrIdx >= --subStr.length() ?
                                        idx - --subStr.length() :
                                        indexOf(str, ++idx, subStr, ++subStrIdx)
  else                              -> indexOf(str, subStrIdx == 0 ? ++idx : idx, subStr, 0)

fun indexOfLast(string str, string subStr)
  indexOfLast(str, --str.length(), subStr, --subStr.length())

fun indexOfLast(string str, int idx, string subStr, int subStrIdx)
  if idx < 0                        -> -1
  if str[idx] == subStr[subStrIdx]  -> subStrIdx <= 0 ?
                                        idx :
                                        indexOfLast(str, --idx, subStr, --subStrIdx)
  else                              -> indexOfLast(
                                          str,
                                          subStrIdx == --subStr.length() ? --idx : idx,
                                          subStr,
                                          --subStr.length())

fun indexOf(string str, delegate{char, bool} pred)
  indexOf(str, 0, pred)

fun indexOf(string str, int idx, delegate{char, bool} pred)
  if idx >= str.length()  -> -1
  if pred(str[idx])       -> idx
  else                    -> indexOf(str, ++idx, pred)

fun startsWith(string str, string subStr)
  startsWithImpl(str, 0, subStr)

fun startsWithImpl(string str, int idx, string subStr)
  if idx >= subStr.length()   -> true
  if idx >= str.length()      -> false
  if str[idx] != subStr[idx]  -> false
  else                        -> startsWithImpl(str, ++idx, subStr)

fun endsWith(string str, string subStr)
  endsWithImpl(str, 0, subStr)

fun endsWithImpl(string str, int revIdx, string subStr)
  if revIdx >= subStr.length()                                          -> true
  if revIdx >= str.length()                                             -> false
  if str[--str.length() - revIdx] != subStr[--subStr.length() - revIdx] -> false
  else -> endsWithImpl(str, ++revIdx, subStr)

fun any(string str, delegate{char, bool} pred)
  anyImpl(str, 0, pred)

fun anyImpl(string str, int idx, delegate{char, bool} pred)
  if idx >= str.length()  -> false
  else                    -> pred(str[idx]) || anyImpl(str, ++idx, pred)

fun all(string str, delegate{char, bool} pred)
  allImpl(str, 0, pred)

fun allImpl(string str, int idx, delegate{char, bool} pred)
  if idx >= str.length()  -> true
  else                    -> pred(str[idx]) && allImpl(str, ++idx, pred)

fun none(string str, delegate{char, bool} pred)
  noneImpl(str, 0, pred)

fun noneImpl(string str, int idx, delegate{char, bool} pred)
  if idx >= str.length()  -> true
  else                    -> !pred(str[idx]) && noneImpl(str, ++idx, pred)

fun replace(string str, string old, string new)
  replaceImpl(str, 0, old, new)

fun replaceImpl(string str, int startIdx, string old, string new)
  idx = str.indexOf(startIdx, old, 0);
  if idx < 0  ->  str
  else        ->  newStr = str[0, idx] + new + str[idx + old.length(), str.length()];
                  replaceImpl(newStr, idx + new.length(), old, new)

fun insert(string str, int idx, string val)
  if idx == 0             -> val + str
  if idx >= str.length()  -> str + val
  else                    -> str[0, idx] + val + str[idx, str.length()]

fun fold{T}(string str, delegate{T, char, T} del)
  foldImpl(str, 0, del, T())

fun foldImpl{T}(string str, int idx, delegate{T, char, T} del, T val)
  idx >= str.length() ? val : foldImpl(str, ++idx, del, del(val, str[idx]))

fun transform(string str, delegate{char, char} del)
  str.fold(lambda (string res, char c) res + del(c))

fun split(string str, delegate{char, bool} pred)
  splitImpl(str, --str.length(), --str.length(), pred, List{string}())

fun splitImpl(string str, int startIdx, int endIdx, delegate{char, bool} pred, List{string} result)
  if startIdx < 0         -> startIdx == endIdx ? result : str[startIdx, ++endIdx] :: result
  if pred(str[startIdx])  -> startIdx == endIdx ?
    splitImpl(str, --startIdx, --endIdx, pred, result) :
    splitImpl(str, --startIdx, --startIdx, pred, str[++startIdx, ++endIdx] :: result)
  else                    -> splitImpl(str, --startIdx, endIdx, pred, result)

fun toChars(string str)
  toCharsImpl(str, --str.length(), List{char}())

fun toCharsImpl(string str, int idx, List{char} result)
  if idx < 0  -> result
  else        -> toCharsImpl(str, --idx, str[idx] :: result)

fun join(List{string} l)
  l.sum()

fun join(List{char} l)
  l.fold(appendChar)

// Tests

assert("".isEmpty() && !" ".isEmpty())

assert(
  "hello world".contains("hello") &&
  "hello world".contains("world") &&
  "hello world".contains(" ") &&
  "hello world".contains("ld") &&
  "hello world".contains("he") &&
  "hello world".contains("o w") &&
  !"hello".contains("world") &&
  !"hello".contains("llow") &&
  !"hello".contains(""))

assert(
  "hello world".indexOf("hello") == 0 &&
  "hello world".indexOf("world") == 6 &&
  "hello world".indexOf(" ") == 5 &&
  "hello world".indexOf("ld") == 9 &&
  "hello world".indexOf("lo") == 3 &&
  "hello world".indexOf("el") == 1 &&
  "hello world".indexOf("he") == 0 &&
  "hello world".indexOf("o w") == 4 &&
  "wasd wasd".indexOf("wasd") == 0 &&
  "hello".indexOf("world") == -1 &&
  "hello".indexOf("llow") == -1 &&
  "hello".indexOf("") == -1)

assert(
  "wasd wasd".indexOfLast("wasd") == 5 &&
  "hello world".indexOfLast("hello") == 0 &&
  "hello world".indexOfLast("world") == 6 &&
  "hello world".indexOfLast("lo") == 3 &&
  "hello world".indexOfLast("el") == 1 &&
  "wasd wasd".indexOfLast(" ") == 4 &&
  "wasd wasd".indexOfLast("d w") == 3 &&
  "wasd wasd".indexOfLast("wasdz") == -1 &&
  "wasd wasd".indexOfLast("") == -1)

assert(
  "hello world".indexOf(equals{char}[' ']) == 5 &&
  "hello world".indexOf(lambda (char c) c == 'd') == 10 &&
  "hello world".indexOf(equals{char}['h']) == 0 &&
  "hello world".indexOf(equals{char}['1']) == -1 &&
  "".indexOf(equals{char}[' ']) == -1)

assert(
  "hello world".startsWith("hello") &&
  "hello world".startsWith("h") &&
  "hello world".startsWith("hello world") &&
  "hello world".startsWith("") &&
  !"hello world".startsWith("world") &&
  !"hello".startsWith("hello world") &&
  !"hello".startsWith("ello") &&
  !"".startsWith("h") &&
  "".startsWith(""))

assert(
  "hello world".endsWith("world") &&
  "hello world".endsWith("d") &&
  "hello world".endsWith("hello world") &&
  "hello world".endsWith("") &&
  !"hello world".endsWith("hello") &&
  !"hello".endsWith("hello world") &&
  !"hello".endsWith("hell") &&
  !"".endsWith("h") &&
  "".endsWith(""))

assert(
  "hello world".any(equals{char}[' ']) &&
  !"hello world".any(equals{char}['1']))

assert(
  "hello".all(!equals{char}[' ']) &&
  !"hello world".all(equals{char}[' ']))

assert(
  "hello world".none(equals{char}['1']) &&
  !"hello world".none(equals{char}[' ']))

assert(
  "hello world".replace("hello", "world") == "world world" &&
  "hello hello hello".replace(" ", "-") == "hello-hello-hello" &&
  "hello hello hello".replace(" ", "") == "hellohellohello" &&
  "hello hello hello".replace("l", "L") == "heLLo heLLo heLLo" &&
  "hello hello hello".replace(" ", " world ") == "hello world hello world hello" &&
  "hello".replace("world", "1337") == "hello" &&
  "hellohellohello".replace("hello", "world") == "worldworldworld" &&
  "hellohellohello".replace("hello", "") == "" &&
  "lelelel".replace("e", "lele") == "llelellelellelel" &&
  "".replace("", "") == "")

assert(
  "helloworld".insert(0, "_") == "_helloworld" &&
  "helloworld".insert("helloworld".length(), "_") == "helloworld_" &&
  "helloworld".insert(1, "_") == "h_elloworld" &&
  "helloworld".insert(5, "_") == "hello_world")

assert(
  "hello".transform(lambda (char c) 'W') == "WWWWW" &&
  "".transform(lambda (char c) 'W') == "")

assert(
  "hello world".split(equals{char}[' ']) == "hello" :: "world" :: List{string}() &&
  "lineA\nlineB\nlineC".split(equals{char}['\n']) == "lineA" :: "lineB" :: "lineC" :: List{string}() &&
  "   hello   world   ".split(equals{char}[' ']) == "hello" :: "world" :: List{string}() &&
  "    ".split(equals{char}[' ']).isEmpty() &&
  " h e l l o ".split(equals{char}[' ']) == "h" :: "e" :: "l" :: "l" :: "o" :: List{string}() &&
  "hello-world".split(equals{char}[' ']) == List("hello-world") &&
  "".split(equals{char}[' ']) == List{string}())

assert(
  "h".toChars() == List{char}('h') &&
  "hello".toChars() == 'h' :: 'e' :: 'l' :: 'l' :: 'o' :: List{char}() &&
  "1337".toChars().join() == "1337" &&
  "".toChars() == List{char}())

assert(
  join("hello" :: " " :: "world" :: List{string}()) == "hello world" &&
  join('h' :: 'e' :: 'l' :: 'l' :: 'o' :: List{char}()) == "hello" &&
  List{string}().join() == "" &&
  List{char}().join() == "")
