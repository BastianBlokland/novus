import "option.nov"

// -- Types

union List{T} = LNode{T},  LEnd

struct LNode{T} = T val, List{T} next
struct LEnd

// -- Constructors

fun List{T}() -> List{T}
  LEnd()

fun List{T}(T val) -> List{T}
  LNode(val, List{T}())

fun List{T}(T val, List{T} next) -> List{T}
  LNode(val, next)

// -- Operators

fun ::{T}(T val, List{T} l)
  l.push(val)

fun ::{T}(List{T} l1, List{T} l2)
  l2.push(l1)

fun ::{T}(Option{T} opt, List{T} l)
  if opt as T val -> l.push(val)
  if opt is None  -> l

fun []{T}(List{T} l, int idx) -> Option{T}
  l.pop(idx).front()

fun []{T}(List{T} l, int startIdx, int endIdx) -> List{T}
  l.pop(startIdx).take(endIdx - startIdx)

// -- Conversions

fun string{T}(List{T} l)
  l.string("[", ",", "]")

fun string{T}(List{T} l, string start, string sep, string end)
  start + l.fold(lambda (string prefix, T v) prefix.length() == 0 ?
    v.string() :
    prefix + sep + v.string()) + end

// -- Functions

fun isEmpty{T}(List{T} l)
  l is LEnd

fun front{T}(List{T} l) -> Option{T}
  l as LNode{T} n ? n.val : None()

fun back{T}(List{T} l) -> Option{T}
  if l as LNode{T} n  -> n.next is LEnd ? n.val : back(n.next)
  if l is LEnd        -> None()

fun push{T}(List{T} l, T val)
  List(val, l)

fun push{T}(List{T} l, List{T} l2)
  if l2 as LNode{T} n -> List{T}(n.val, push(l, n.next))
  if l2 is LEnd       -> l

fun pushRange{T}(List{T} l, T start, T end)
  if start >= end -> l
  else            -> pushRange(--end :: l, start, --end)

fun pushBack{T}(List{T} l, T val)
  if l as LNode{T} n  -> List(n.val, n.next.pushBack(val))
  if l is LEnd        -> List(val)

fun pop{T}(List{T} l)
  pop(l, 1)

fun pop{T}(List{T} l, int amount)
  if l as LNode{T} n  -> amount > 0 ? pop(n.next, --amount) : l
  if l is LEnd        -> l

fun pop{T}(List{T} l, function{T, bool} pred)
  if l as LNode{T} n  -> pred(n.val) ? pop(n.next, pred) : l
  if l is LEnd        -> l

fun popBack{T}(List{T} l)
  popBack(l, 1)

fun popBack{T}(List{T} l, int amount)
  l.take(l.length() - amount)

fun take{T}(List{T} l, int amount)
  if l as LNode{T} n  -> amount > 0 ? List{T}(n.val, take(n.next, --amount)) : List{T}()
  if l is LEnd        -> l

fun take{T}(List{T} l, function{T, bool} pred)
  if l as LNode{T} n  -> pred(n.val) ? List{T}(n.val, take(n.next, pred)) : List{T}()
  if l is LEnd        -> l

fun insert{T}(List{T} l, int idx, T val)
  l.take(idx) :: val :: l.pop(idx)

fun insertOrdered{T}(List{T} l, T val)
  if l as LNode{T} n -> val < n.val ? List(val, l) : List(n.val, insertOrdered(n.next, val))
  if l is LEnd       -> List(val)

fun rangeList{T}(T start, T end)
  List{T}().pushRange(start, end)

fun fold{T, TResult}(List{T} l, function{TResult, T, TResult} func)
  fold(l, func, TResult())

fun fold{T, TResult}(List{T} l, function{TResult, T, TResult} func, TResult result)
  if l as LNode{T} n -> fold(n.next, func, func(result, n.val))
  if l is LEnd       -> result

fun foldRight{T, TResult}(List{T} l, function{TResult, T, TResult} func)
  foldRight(l, func, TResult())

fun foldRight{T, TResult}(List{T} l, function{TResult, T, TResult} func, TResult result)
  if l as LNode{T} n -> func(foldRight(n.next, func, result), n.val)
  if l is LEnd       -> result

fun filter{T}(List{T} l, function{T, bool} pred)
  l.foldRight(lambda (List{T} newList, T val) pred(val) ? val :: newList : newList)

fun filterReverse{T}(List{T} l, function{T, bool} pred)
  l.fold(lambda (List{T} newList, T val) pred(val) ? val :: newList : newList)

fun filterNone{T}(List{Option{T}} l)
  l.foldRight(lambda (List{T} newList, Option{T} val) val as T t ? t :: newList : newList)

fun filterNoneReverse{T}(List{Option{T}} l)
  l.fold(lambda (List{T} newList, Option{T} val) val as T t ? t :: newList : newList)

fun map{T, TResult}(List{T} l, function{T, TResult} func)
  l.foldRight(lambda (List{TResult} newList, T val) func(val) :: newList)

fun mapReverse{T, TResult}(List{T} l, function{T, TResult} func)
  l.fold(lambda (List{TResult} newList, T val) func(val) :: newList)

fun length{T}(List{T} l)
  l.fold(lambda (int c, T val) c + 1)

fun sum{T}(List{T} l)
  l.fold(lambda (T s, T val) s + val)

fun reverse{T}(List{T} l)
  l.fold(lambda (List{T} newList, T val) val :: newList)

fun sort{T}(List{T} l)
  l.fold(insertOrdered{T})

fun count{T}(List{T} l, function{T, bool} pred)
  l.fold(lambda (int c, T val) pred(val) ? ++c : c)

fun any{T}(List{T} l, function{T, bool} pred)
  if l as LNode{T} n  -> pred(n.val) || n.next.any(pred)
  if l is LEnd        -> false

fun all{T}(List{T} l, function{T, bool} pred)
  if l as LNode{T} n  -> pred(n.val) && n.next.any(pred)
  if l is LEnd        -> true

fun none{T}(List{T} l, function{T, bool} pred)
  if l as LNode{T} n  -> !pred(n.val) && n.next.none(pred)
  if l is LEnd        -> true

fun contains{T}(List{T} l, T item)
  if l as LNode{T} n  -> n.val == item || n.next.contains(item)
  if l is LEnd        -> false

fun for{T, TResult}(T from, T to, function{T, TResult} func)
  rangeList(from, to).map(func)

fun for{T, TResult}(T to, function{T, TResult} func)
  rangeList(T(), to).map(func)

// -- Impure

act fold{T, TResult}(List{T} l, action{TResult, T, TResult} a)
  fold(l, a, TResult())

act fold{T, TResult}(List{T} l, action{TResult, T, TResult} a, TResult result)
  if l as LNode{T} n -> fold(n.next, a, a(result, n.val))
  if l is LEnd       -> result

act foldRight{T, TResult}(List{T} l, action{TResult, T, TResult} a)
  foldRight(l, a, TResult())

act foldRight{T, TResult}(List{T} l, action{TResult, T, TResult} a, TResult result)
  if l as LNode{T} n -> a(foldRight(n.next, a, result), n.val)
  if l is LEnd       -> result

act map{T, TResult}(List{T} l, action{T, TResult} a)
  l.foldRight(impure lambda (List{TResult} newList, T val) a(val) :: newList)

act mapReverse{T, TResult}(List{T} l, action{T, TResult} a)
  l.fold(impure lambda (List{TResult} newList, T val) a(val) :: newList)

act for{T, TResult}(T from, T to, action{T, TResult} a)
  rangeList(from, to).map(a)

act for{T, TResult}(T to, action{T, TResult} a)
  rangeList(T(), to).map(a)

// -- Tests

assert(List(42) == List(42))
assert(List(42) != List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l == 1 :: 2 :: List(3) &&
  l != 3 :: 2 :: 1 :: List{int}() &&
  l != List(42))

assert(Option(42) :: List{int}() == List(42))
assert(Option{int}() :: List{int}() == List{int}())

assert(
  l1 = 1 :: 2 :: 3 :: List{int}();
  l2 = 10 :: 9 :: 8 :: List{int}();
  l1 :: l2 :: l1 == 1 :: 2 :: 3 :: 10 :: 9 :: 8 :: 1 :: 2 :: 3 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l[-1] == 1 && l[0] == 1 && l[1] == 2 && l[2] == 3 && l[4] == None())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l[0, -1] == List{int}() &&
  l[0, 0] == List{int}() &&
  l[0, 1] == 1 :: List{int}() &&
  l[0, 2] == 1 :: 2 :: List{int}() &&
  l[0, 3] == 1 :: 2 :: 3 :: List{int}() &&
  l[0, 4] == 1 :: 2 :: 3 :: List{int}() &&
  l[1, 4] == 2 :: 3 :: List{int}() &&
  l[3, 3] == List{int}() &&
  l[3, 4] == List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.string() == "[1,2,3]" && l.string("", "", "") == "123")
assert(
  l = "hello" :: "world" :: List{string}();
  l.string() == "[hello,world]" && l.string("", "", "") == "helloworld")
assert(string(List{int}()) == "[]")

assert(List{int}().isEmpty() && !List{int}(42).isEmpty() && List{int}(42).pop().isEmpty())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.front() ?? -1 == 1 && l.back() ?? -1 == 3)
assert(
  l = List{int}();
  l.front() is None && l.back() is None)

assert(push(List{int}(), 42) == List(42))
assert(push(2 :: 3 :: List{int}(), 1) == 1 :: 2 :: 3 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.push(List(42)) == 42 :: 1 :: 2 :: 3 :: List{int}() &&
  l.push(42 :: 1337 :: List{int}()) == 42 :: 1337 :: 1 :: 2 :: 3 :: List{int}() &&
  l.push(3 :: 2 :: 1 :: List{int}()) == 3 :: 2 :: 1 :: 1 :: 2 :: 3 :: List{int}())

assert(
  l = 42 :: 1337 :: List{int}();
  l.pushRange(1, 3) == 1 :: 2 :: 42 :: 1337 :: List{int}())

assert(pushBack(List{int}(), 42) == List(42))
assert(pushBack(1 :: 2 :: List{int}(), 3) == 1 :: 2 :: 3 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: 4 :: List{int}();
  l.pop() == 2 :: 3 :: 4 :: List{int}() &&
  l.pop(-1) == l &&
  l.pop(0) == l &&
  l.pop(1) == 2 :: 3 :: 4 :: List{int}() &&
  l.pop(2) == 3 :: 4 :: List{int}() &&
  l.pop(3) == 4 :: List{int}() &&
  l.pop(4) == List{int}() &&
  l.pop(5) == List{int}())

assert(
  l = 1 :: 2 :: 3 :: 4 :: List{int}();
  l.pop(lambda (int i) i == 0) == l &&
  l.pop(lambda (int i) i > 0) == List{int}() &&
  l.pop(lambda (int i) i == 1) == 2 :: 3 :: 4 :: List{int}() &&
  l.pop(lambda (int i) i < 3) == 3 :: 4 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.take(-1) == List{int}() &&
  l.take(0) == List{int}() &&
  l.take(1) == 1 :: List{int}() &&
  l.take(2) == 1 :: 2 :: List{int}() &&
  l.take(3) == 1 :: 2 :: 3 :: List{int}() &&
  l.take(4) == 1 :: 2 :: 3 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.take(lambda (int i) i == 0) == List{int}() &&
  l.take(lambda (int i) i > 0) == l &&
  l.take(lambda (int i) i < 3) == 1 :: 2 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: 4 :: List{int}();
  l.popBack() == 1 :: 2 :: 3 :: List{int}() &&
  l.popBack(-1) == l &&
  l.popBack(0) == l &&
  l.popBack(1) == 1 :: 2 :: 3 :: List{int}() &&
  l.popBack(2) == 1 :: 2 :: List{int}() &&
  l.popBack(3) == 1 :: List{int}() &&
  l.popBack(4) == List{int}() &&
  l.popBack(5) == List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.insert(-1, 42) == 42 :: 1 :: 2 :: 3 :: List{int}() &&
  l.insert(0, 42) == 42 :: 1 :: 2 :: 3 :: List{int}() &&
  l.insert(1, 42) == 1 :: 42 :: 2 :: 3 :: List{int}() &&
  l.insert(2, 42) == 1 :: 2 :: 42 :: 3 :: List{int}() &&
  l.insert(3, 42) == 1 :: 2 :: 3 :: 42 :: List{int}() &&
  l.insert(4, 42) == 1 :: 2 :: 3 :: 42 :: List{int}())

assert(
  l = 1 :: 4 :: 5 :: List{int}();
  l.insertOrdered(0) == 0 :: 1 :: 4 :: 5 :: List{int}() &&
  l.insertOrdered(1) == 1 :: 1 :: 4 :: 5 :: List{int}() &&
  l.insertOrdered(2) == 1 :: 2 :: 4 :: 5 :: List{int}() &&
  l.insertOrdered(6) == 1 :: 4 :: 5 :: 6 :: List{int}() &&
  l.insertOrdered(99) == 1 :: 4 :: 5 :: 99 :: List{int}())

assert(rangeList(1, 4) == 1 :: 2 :: 3 :: List{int}())
assert(rangeList('a', 'd') == 'a' :: 'b' :: 'c' :: List{char}())

assert(
  l = 1 :: 2 :: 3 :: 4 :: List{int}();
  l.filter(lambda (int v) v > 2) == 3 :: 4 :: List{int}() &&
  l.filter(lambda (int v) v < 0) == List{int}())

assert(
  l = 1 :: 2 :: 3 :: 4 :: 5 :: List{int}();
  l.filterReverse(lambda (int v) v > 2) == 5 :: 4 :: 3 :: List{int}() &&
  l.filterReverse(lambda (int v) v < 0) == List{int}())

assert(
  l = Option(1) :: Option{int}() :: Option(2) :: Option{int}() :: List{Option{int}}();
  l.filterNone() == 1 :: 2 :: List{int}())

assert(
  l = Option(1) :: Option{int}() :: Option(2) :: Option{int}() :: List{Option{int}}();
  l.filterNoneReverse() == 2 :: 1 :: List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.map(lambda (int v) v * 2) == 2 :: 4 :: 6 :: List{int}() &&
  l.map(lambda (int v) v.string()) == "1" :: "2" :: "3" :: List{string}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.mapReverse(lambda (int v) v * 2) == 6 :: 4 :: 2 :: List{int}() &&
  l.mapReverse(lambda (int v) v.string()) == "3" :: "2" :: "1" :: List{string}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.length() == 3)
assert(length(List{int}()) == 0)

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.sum() == 6)
assert(
  l = 0.1 :: 1.0 :: 1.1 :: List{float}();
  l.sum() == 2.2)
assert(
  l = "hello" :: " " :: "world" :: List{string}();
  l.sum() == "hello world")
assert(List{int}().sum() == 0)

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.reverse() == 3 :: 2 :: 1 :: List{int}())
assert(List{int}().reverse() == List{int}())

assert(
  l = 42 :: 1337 :: -42 :: -5 :: List{int}();
  l.sort() == -42 :: -5 :: 42 :: 1337 :: List{int}())
assert(List{int}().sort() == List{int}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.count(lambda (int v) v > 2) == 1 &&
  l.count(lambda (int v) v == 0) == 0 &&
  l.count(lambda (int v) v > 0) == 3)

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.any(lambda (int v) v > 2) && !l.any(lambda (int v) v < 0))

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.all(lambda (int v) v > 0) && !l.all(lambda (int v) v > 1))

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.none(lambda (int v) v < 0) && !l.none(lambda (int v) v > 1))

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.contains(1) &&
  l.contains(2) &&
  l.contains(3) &&
  !l.contains(0) &&
  !l.contains(4))

assert(for(1, 4, lambda (int i) i * i) == 1 :: 4 :: 9 :: List{int}())

assert(for(4, lambda (int i) i * i) == 0 :: 1 :: 4 :: 9 :: List{int}())

// -- Impure Tests

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.map(impure lambda (int v) v * 2) == 2 :: 4 :: 6 :: List{int}() &&
  l.map(impure lambda (int v) v.string()) == "1" :: "2" :: "3" :: List{string}())

assert(
  l = 1 :: 2 :: 3 :: List{int}();
  l.mapReverse(impure lambda (int v) v * 2) == 6 :: 4 :: 2 :: List{int}() &&
  l.mapReverse(impure lambda (int v) v.string()) == "3" :: "2" :: "1" :: List{string}())

assert(for(1, 4, impure lambda (int i) i * i) == 1 :: 4 :: 9 :: List{int}())

assert(for(4, impure lambda (int i) i * i) == 0 :: 1 :: 4 :: 9 :: List{int}())
