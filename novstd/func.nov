// -- Operators

fun []{TC, TR}(function{TC, TR} func, TC arg)
  curry(func, arg)

fun []{T1, TC, TR}(function{T1, TC, TR} func, TC arg)
  curry(func, arg)

fun []{T1, T2, TC, TR}(function{T1, T2, TC, TR} func, TC arg)
  curry(func, arg)

fun []{T1, T2, T3, TC, TR}(function{T1, T2, T3, TC, TR} func, TC arg)
  curry(func, arg)

fun []{TC, TR}(action{TC, TR} a, TC arg)
  curry(a, arg)

fun []{T1, TC, TR}(action{T1, TC, TR} a, TC arg)
  curry(a, arg)

fun []{T1, T2, TC, TR}(action{T1, T2, TC, TR} a, TC arg)
  curry(a, arg)

fun []{T1, T2, T3, TC, TR}(action{T1, T2, T3, TC, TR} a, TC arg)
  curry(a, arg)

fun &(function{bool} predA, function{bool} predB)
  and(predA, predB)

fun &{T1}(function{T1, bool} predA, function{T1, bool} predB)
  and(predA, predB)

fun &{T1, T2}(function{T1, T2, bool} predA, function{T1, T2, bool} predB)
  and(predA, predB)

fun &{T1, T2, T3}(function{T1, T2, T3, bool} predA, function{T1, T2, T3, bool} predB)
  and(predA, predB)

fun &{T1, T2, T3, T4}(function{T1, T2, T3, T4, bool} predA, function{T1, T2, T3, T4, bool} predB)
  and(predA, predB)

fun |(function{bool} predA, function{bool} predB)
  or(predA, predB)

fun |{T1}(function{T1, bool} predA, function{T1, bool} predB)
  or(predA, predB)

fun |{T1, T2}(function{T1, T2, bool} predA, function{T1, T2, bool} predB)
  or(predA, predB)

fun |{T1, T2, T3}(function{T1, T2, T3, bool} predA, function{T1, T2, T3, bool} predB)
  or(predA, predB)

fun |{T1, T2, T3, T4}(function{T1, T2, T3, T4, bool} predA, function{T1, T2, T3, T4, bool} predB)
  or(predA, predB)

fun !(function{bool} pred)
  invert(pred)

fun !{T1}(function{T1, bool} pred)
  invert(pred)

fun !{T1, T2}(function{T1, T2, bool} pred)
  invert(pred)

fun !{T1, T2, T3}(function{T1, T2, T3, bool} pred)
  invert(pred)

fun !{T1, T2, T3, T4}(function{T1, T2, T3, T4, bool} pred)
  invert(pred)

// -- Functions

fun equals{T}(T a, T b)
  a == b

fun construct{TR}()
  TR()

fun construct{T1, TR}(T1 a)
  TR(a)

fun construct{T1, T2, TR}(T1 a, T2 b)
  TR(a, b)

fun construct{T1, T2, T3, TR}(T1 a, T2 b, T3 c)
  TR(a, b, c)

fun construct{T1, T2, T3, T4, TR}(T1 a, T2 b, T3 c, T4 d)
  TR(a, b, c, d)

fun construct{T1, T2, T3, T4, T5, TR}(T1 a, T2 b, T3 c, T4 d, T5 e)
  TR(a, b, c, d, e)

fun curry{TC, TR}(function{TC, TR} func, TC arg)
  lambda () func(arg)

fun curry{T1, TC, TR}(function{T1, TC, TR} func, TC arg)
  lambda (T1 a1) func(a1, arg)

fun curry{T1, T2, TC, TR}(function{T1, T2, TC, TR} func, TC arg)
  lambda (T1 a1, T2 a2) func(a1, a2, arg)

fun curry{T1, T2, T3, TC, TR}(function{T1, T2, T3, TC, TR} func, TC arg)
  lambda (T1 a1, T2 a2, T3 a3) func(a1, a2, a3, arg)

fun curry{TC, TR}(action{TC, TR} a, TC arg)
  impure lambda () a(arg)

fun curry{T1, TC, TR}(action{T1, TC, TR} a, TC arg)
  impure lambda (T1 a1) a(a1, arg)

fun curry{T1, T2, TC, TR}(action{T1, T2, TC, TR} a, TC arg)
  impure lambda (T1 a1, T2 a2) a(a1, a2, arg)

fun curry{T1, T2, T3, TC, TR}(action{T1, T2, T3, TC, TR} a, TC arg)
  impure lambda (T1 a1, T2 a2, T3 a3) a(a1, a2, a3, arg)

fun and(function{bool} predA, function{bool} predB)
  lambda () predA() && predB()

fun and{T1}(function{T1, bool} predA, function{T1, bool} predB)
  lambda (T1 a1) predA(a1) && predB(a1)

fun and{T1, T2}(function{T1, T2, bool} predA, function{T1, T2, bool} predB)
  lambda (T1 a1, T2 a2) predA(a1, a2) && predB(a1, a2)

fun and{T1, T2, T3}(function{T1, T2, T3, bool} predA, function{T1, T2, T3, bool} predB)
  lambda (T1 a1, T2 a2, T3 a3) predA(a1, a2, a3) && predB(a1, a2, a3)

fun and{T1, T2, T3, T4}(function{T1, T2, T3, T4, bool} predA, function{T1, T2, T3, T4, bool} predB)
  lambda (T1 a1, T2 a2, T3 a3, T4 a4) predA(a1, a2, a3, a4) && predB(a1, a2, a3, a4)

fun or(function{bool} predA, function{bool} predB)
  lambda () predA() || predB()

fun or{T1}(function{T1, bool} predA, function{T1, bool} predB)
  lambda (T1 a1) predA(a1) || predB(a1)

fun or{T1, T2}(function{T1, T2, bool} predA, function{T1, T2, bool} predB)
  lambda (T1 a1, T2 a2) predA(a1, a2) || predB(a1, a2)

fun or{T1, T2, T3}(function{T1, T2, T3, bool} predA, function{T1, T2, T3, bool} predB)
  lambda (T1 a1, T2 a2, T3 a3) predA(a1, a2, a3) || predB(a1, a2, a3)

fun or{T1, T2, T3, T4}(function{T1, T2, T3, T4, bool} predA, function{T1, T2, T3, T4, bool} predB)
  lambda (T1 a1, T2 a2, T3 a3, T4 a4) predA(a1, a2, a3, a4) || predB(a1, a2, a3, a4)

fun invert(function{bool} pred)
  lambda () !pred()

fun invert{T1}(function{T1, bool} pred)
  lambda (T1 a1) !pred(a1)

fun invert{T1, T2}(function{T1, T2, bool} pred)
  lambda (T1 a1, T2 a2) !pred(a1, a2)

fun invert{T1, T2, T3}(function{T1, T2, T3, bool} pred)
  lambda (T1 a1, T2 a2, T3 a3) !pred(a1, a2, a3)

fun invert{T1, T2, T3, T4}(function{T1, T2, T3, T4, bool} pred)
  lambda (T1 a1, T2 a2, T3 a3, T4 a4) !pred(a1, a2, a3, a4)

fun invoke{TR}(function{TR} func)
  func()

fun invoke{T1, TR}(function{T1, TR} func, T1 a1)
  func(a1)

fun invoke{T1, T2, TR}(function{T1, T2, TR} func, T1 a1, T2 a2)
  func(a1, a2)

fun invoke{T1, T2, T3, TR}(function{T1, T2, T3, TR} func, T1 a1, T2 a2, T3 a3)
  func(a1, a2, a3)

fun invoke{T1, T2, T3, T4, TR}(function{T1, T2, T3, T4, TR} func, T1 a1, T2 a2, T3 a3, T4 a4)
  func(a1, a2, a3, a4)

fun invoke{T1, T2, T3, T4, T5, TR}(function{T1, T2, T3, T4, T5, TR} func, T1 a1, T2 a2, T3 a3, T4 a4, T5 a5)
  func(a1, a2, a3, a4, a5)

// -- Actions

act invoke{TR}(action{TR} func)
  func()

act invoke{T1, TR}(action{T1, TR} func, T1 a1)
  func(a1)

act invoke{T1, T2, TR}(action{T1, T2, TR} func, T1 a1, T2 a2)
  func(a1, a2)

act invoke{T1, T2, T3, TR}(action{T1, T2, T3, TR} func, T1 a1, T2 a2, T3 a3)
  func(a1, a2, a3)

act invoke{T1, T2, T3, T4, TR}(action{T1, T2, T3, T4, TR} func, T1 a1, T2 a2, T3 a3, T4 a4)
  func(a1, a2, a3, a4)

act invoke{T1, T2, T3, T4, T5, TR}(action{T1, T2, T3, T4, T5, TR} func, T1 a1, T2 a2, T3 a3, T4 a4, T5 a5)
  func(a1, a2, a3, a4, a5)

// -- Tests

assert(equals(1, 1) && !equals(1, 2))

assert(
  eq42 = equals{int}[42];
  eq42(42) && !eq42(43))

assert(
  funcRet = (lambda (int x) x);
  ret42 = funcRet[42];
  ret42() == funcRet(42))

assert(
  funcPlus = (lambda (int a, int b) a + b);
  plus42 = funcPlus[42];
  plus42(1337) == funcPlus(1337, 42))

assert(
  funcSum = (lambda (int a, int b, int c) a + b + c);
  sumPlus42 = funcSum[42];
  sumPlus42(1, 2) == funcSum(1, 2, 42))

assert(
  funcSum = (lambda (int a, int b, int c, int d) a + b + c + d);
  sumPlus42 = funcSum[42];
  sumPlus42(1, 2, 3) == funcSum(1, 2, 3, 42))

assert(
  funcTrue = (lambda () true);
  funcFalse = (lambda () false);
  (funcTrue & funcTrue)() && !(funcTrue & funcFalse)())

assert(
  isEven = (lambda (int x) x % 2 == 0);
  isPos = (lambda (int x) x > 0);
  isEvenAndPos = isEven & isPos;
  isEvenAndPos(2) && !isEvenAndPos(3) && !isEvenAndPos(-2))

assert(
  isEven = (lambda (int x, int y) x % 2 == 0 && y % 2 == 0);
  isPos = (lambda (int x, int y) x > 0 && y > 0);
  isEvenAndPos = isEven & isPos;
  isEvenAndPos(2, 4) && !isEvenAndPos(2, 3) && !isEvenAndPos(2, -2))

assert(
  isEven = (lambda (int x, int y, int z) x % 2 == 0 && y % 2 == 0 && z % 2 == 0);
  isPos = (lambda (int x, int y, int z) x > 0 && y > 0 && z > 0);
  isEvenAndPos = isEven & isPos;
  isEvenAndPos(2, 2, 4) && !isEvenAndPos(2, 2, 3) && !isEvenAndPos(2, 2, -2))

assert(
  isEven = (
    lambda (int x, int y, int z, int w)
      x % 2 == 0 && y % 2 == 0 && z % 2 == 0 && w % 2 == 0
  );
  isPos = (
    lambda (int x, int y, int z, int w)
      x > 0 && y > 0 && z > 0 && w > 0
  );
  isEvenAndPos = isEven & isPos;
  isEvenAndPos(2, 2, 2, 4) && !isEvenAndPos(2, 2, 2, 3) && !isEvenAndPos(2, 2, 2, -2))

assert(
  funcTrue = (lambda () true);
  funcFalse = (lambda () false);
  (funcTrue | funcFalse)() && (funcFalse | funcTrue)() && !(funcFalse | funcFalse)())

assert(
  isEven = (lambda (int x) x % 2 == 0);
  isPos = (lambda (int x) x > 0);
  isEvenOrPos = isEven | isPos;
  isEvenOrPos(2) && isEvenOrPos(1) && isEvenOrPos(-2) && !isEvenOrPos(-1))

assert(
  isEven = (
    lambda (int x, int y)
      x % 2 == 0 && y % 2 == 0
  );
  isPos = (
    lambda (int x, int y)
      x > 0 && y > 0
  );
  isEvenOrPos = isEven | isPos;
  isEvenOrPos(2, 1) && isEvenOrPos(-2, 2) && !isEvenOrPos(-1, -2))

assert(
  isEven = (
    lambda (int x, int y, int z)
      x % 2 == 0 && y % 2 == 0 && z % 2 == 0
  );
  isPos = (
    lambda (int x, int y, int z)
      x > 0 && y > 0 && z > 0
  );
  isEvenOrPos = isEven | isPos;
  isEvenOrPos(2, 2, 1) && isEvenOrPos(-2, -2, 2) && !isEvenOrPos(-1, -1, -2))

assert(
  isEven = (
    lambda (int x, int y, int z, int w)
      x % 2 == 0 && y % 2 == 0 && z % 2 == 0 && w % 2 == 0
  );
  isPos = (
    lambda (int x, int y, int z, int w)
      x > 0 && y > 0 && z > 0 && w > 0
  );
  isEvenOrPos = isEven | isPos;
  isEvenOrPos(2, 2, 2, 1) && isEvenOrPos(-2, -2, -2, 2) && !isEvenOrPos(-1, -1, -1, -2))

assert(
  funcFalse = (lambda () false);
  funcTrue = !funcFalse;
  funcTrue())

assert(
  is42 = (lambda (int x) x == 42);
  isNot42 = !is42;
  isNot42(1337) && !isNot42(42))

assert(
  eq = (lambda (int x, int y) x == y);
  notEq = !eq;
  notEq(42, 1337) && !notEq(42, 42))

assert(
  eq = (
    lambda (int x, int y, int z)
      x == y && y == z
  );
  notEq = !eq;
  notEq(42, 42, 1337) && !notEq(42, 42, 42))

assert(
  eq = (
    lambda (int x, int y, int z, int w)
      x == y && y == z && z == w
  );
  notEq = !eq;
  notEq(42, 42, 42, 1337) && !notEq(42, 42, 42, 42))

assert(invoke(lambda() 42 + 1337) == 42 + 1337)

assert(invoke(lambda(int x) 42 + x, 1337) == 42 + 1337)

assert(invoke(lambda(int x, float y) string(x) + string(y), 1337, 42.42) == "133742.42")

assert(
  invoke(lambda(int x, float y, string z) string(x) + " " + string(y) + " " + z,
    1337, 42.42, "hello world") == "1337 42.42 hello world")

assert(
  invoke(lambda(int x, float y, string z, long w) string(x) + " " + string(y) + " " + z + " " + string(w),
    1337, 42.42, "hello world", -4242L) == "1337 42.42 hello world -4242")

assert(
  invoke(
    lambda(int x, float y, string z, long w, int a)
      string(x) + " " + string(y) + " " + z + " " + string(w) + " " + string(a)
    ,1337, 42.42, "hello world", -4242L, -1) == "1337 42.42 hello world -4242 -1")

// -- Impure tests

assert(
  actRet = (impure lambda (int x) x);
  ret42 = actRet[42];
  ret42() == actRet(42))

assert(
  actPlus = (impure lambda (int a, int b) a + b);
  plus42 = actPlus[42];
  plus42(1337) == actPlus(1337, 42))

assert(
  actSum = (impure lambda (int a, int b, int c) a + b + c);
  sumPlus42 = actSum[42];
  sumPlus42(1, 2) == actSum(1, 2, 42))

assert(
  actSum = (impure lambda (int a, int b, int c, int d) a + b + c + d);
  sumPlus42 = actSum[42];
  sumPlus42(1, 2, 3) == actSum(1, 2, 3, 42))

assert(invoke(impure lambda() 42 + 1337) == 42 + 1337)

assert(invoke(impure lambda(int x) 42 + x, 1337) == 42 + 1337)

assert(invoke(impure lambda(int x, float y) string(x) + string(y), 1337, 42.42) == "133742.42")

assert(
  invoke(impure lambda(int x, float y, string z) string(x) + " " + string(y) + " " + z,
    1337, 42.42, "hello world") == "1337 42.42 hello world")

assert(
  invoke(impure lambda(int x, float y, string z, long w) string(x) + " " + string(y) + " " + z + " " + string(w),
    1337, 42.42, "hello world", -4242L) == "1337 42.42 hello world -4242")

assert(
  invoke(
    impure lambda(int x, float y, string z, long w, int a)
      string(x) + " " + string(y) + " " + z + " " + string(w) + " " + string(a)
    , 1337, 42.42, "hello world", -4242L, -1) == "1337 42.42 hello world -4242 -1")
