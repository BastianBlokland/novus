import "std/core.ns"
import "std/prim.ns"

// -- Actions

act hasEnvVar(string name) -> bool
  intrinsic{env_variable_exists}(name)

act getEnvVar(string name) -> Option{string}
  if hasEnvVar(name)  -> string(intrinsic{env_variable}(name))
  else                -> None()

act getEnvArgs() -> List{string}
  invoke(
    impure lambda (int idx, List{string} result)
      if idx < 0  -> result
      else        -> self(--idx, intrinsic{env_argument}(idx) :: result)
    , --intrinsic{env_argument_count}(), List{string}())

act getHomePath() -> Either{Path, Error}
  homeEnv = getEnvVar("USERPROFILE") ?? getEnvVar("HOME") ?? getEnvVar("HOMEPATH");
  if homeEnv is None            -> Error("Home environment variable not found")
  if homeEnv as string homeStr  -> pathParser().run(homeStr) ! Error("Invalid home environment variable: '" + homeStr + '\'')
