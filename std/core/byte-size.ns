import "std/diag.ns"
import "std/format.ns"
import "std/prim.ns"

// -- Types

enum ByteSizeUnit = B, KiB, MiB, GiB, TiB, PiB

struct ByteSize = long bytes

// -- Constructors

fun ByteSize() ByteSize(0)

fun noinline ByteSize(ByteSizeUnit unit)
  if unit == ByteSizeUnit.KiB -> kibibyte()
  if unit == ByteSizeUnit.MiB -> mebibyte()
  if unit == ByteSizeUnit.GiB -> gibibyte()
  if unit == ByteSizeUnit.TiB -> tebibyte()
  if unit == ByteSizeUnit.PiB -> pebibyte()
  else                        -> byte()

// -- Operators

fun -(ByteSize s)  ByteSize(-s.bytes)
fun -(ByteSize s1, ByteSize s2)   ByteSize(s1.bytes - s2.bytes)
fun +(ByteSize s1, ByteSize s2)   ByteSize(s1.bytes + s2.bytes)
fun *(ByteSize s, long l)         ByteSize(s.bytes * l)
fun /(ByteSize s1, ByteSize s2)   s1.bytes / s2.bytes
fun /(ByteSize s1, long l)        ByteSize(s1.bytes / l)
fun %(ByteSize s1, ByteSize s2)   ByteSize(s1.bytes % s2.bytes)
fun <(ByteSize s1, ByteSize s2)   s1.bytes < s2.bytes
fun >(ByteSize s1, ByteSize s2)   s1.bytes > s2.bytes

// -- Utilities

fun minByteSize() ByteSize(minVal{long}())
fun maxByteSize() ByteSize(maxVal{long}())

fun absByteSize(ByteSize s)
  if s.bytes > 0L       -> s
  if s == minByteSize() -> maxByteSize()
  else                  -> ByteSize(-s.bytes)

fun byte()      ByteSize(1)
fun kibibyte()  byte()      * 1024
fun mebibyte()  kibibyte()  * 1024
fun gibibyte()  mebibyte()  * 1024
fun tebibyte()  gibibyte()  * 1024
fun pebibyte()  tebibyte()  * 1024

fun bytes(long count)      byte()     * count
fun kibibytes(long count)  kibibyte() * count
fun mebibytes(long count)  mebibyte() * count
fun gibibytes(long count)  gibibyte() * count
fun tebibytes(long count)  tebibyte() * count
fun pebibytes(long count)  pebibyte() * count

fun bytes(ByteSize s)     s.bytes
fun kibibytes(ByteSize s) s.bytes / kibibyte().bytes
fun mebibytes(ByteSize s) s.bytes / mebibyte().bytes
fun gibibytes(ByteSize s) s.bytes / gibibyte().bytes
fun tebibytes(ByteSize s) s.bytes / tebibyte().bytes
fun pebibytes(ByteSize s) s.bytes / pebibyte().bytes

fun noinline byteSizeUnit(ByteSize s)
  absSize = absByteSize(s);
  if absSize < kibibyte() -> ByteSizeUnit.B
  if absSize < mebibyte() -> ByteSizeUnit.KiB
  if absSize < gibibyte() -> ByteSizeUnit.MiB
  if absSize < tebibyte() -> ByteSizeUnit.GiB
  if absSize < pebibyte() -> ByteSizeUnit.TiB
  else                    -> ByteSizeUnit.PiB

fun string(ByteSize s)
  byteSizeWriter().run(s)

// -- Writers

// Create a human readable representation.
fun byteSizeWriter(Writer{None} sepWriter = noneWriter()) -> Writer{ByteSize}
  (
    txtFloatWriter(1, FloatToStringFlags.NeverScientific) & sepWriter & enumKeyWriter(Type{ByteSizeUnit}())
  ).map(lambda (ByteSize bs)
    unit = byteSizeUnit(bs);
    Tuple(bs.bytes.float() / ByteSize(unit).bytes.float(), unit)
  )

// -- Tests

assertEq(byte().bytes(), 1)
assertEq(kibibyte().kibibytes(), 1)
assertEq(mebibyte().mebibytes(), 1)
assertEq(gibibyte().gibibytes(), 1)
assertEq(tebibyte().tebibytes(), 1)
assertEq(pebibyte().pebibytes(), 1)

assertEq(kibibyte().bytes(), 1_024L)
assertEq(mebibyte().bytes(), 1_048_576L)
assertEq(gibibyte().bytes(), 1_073_741_824L)
assertEq(tebibyte().bytes(), 1_099_511_627_776L)
assertEq(pebibyte().bytes(), 1_125_899_906_842_624L)

assertEq(bytes(42).string(), "42.0B")
assertEq(kibibytes(42).string(), "42.0KiB")
assertEq(mebibytes(42).string(), "42.0MiB")
assertEq(gibibytes(42).string(), "42.0GiB")
assertEq(tebibytes(42).string(), "42.0TiB")
assertEq(pebibytes(42).string(), "42.0PiB")

assertEq(bytes(-42).string(), "-42.0B")
assertEq(kibibytes(-42).string(), "-42.0KiB")
assertEq(mebibytes(-42).string(), "-42.0MiB")
assertEq(gibibytes(-42).string(), "-42.0GiB")
assertEq(tebibytes(-42).string(), "-42.0TiB")
assertEq(pebibytes(-42).string(), "-42.0PiB")

assertEq(absByteSize(tebibytes(-42)), tebibytes(42))
assertEq(absByteSize(minByteSize()), maxByteSize())
