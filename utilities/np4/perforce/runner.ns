import "std.ns"

import "data/change.ns"
import "data/config.ns"
import "data/file.ns"
import "data/resolve.ns"
import "data/stream.ns"
import "data/var.ns"

// -- Types.

enum P4RunCode = Stat, Info, Error

struct P4RunOutput{T} =
  P4RunCode code,
  T         data

// -- Utilities.

fun p4GlobalArgs(P4Config cfg) -> List{string}
  "-p"  :: cfg.server.string() ::
  "-c"  :: cfg.client.string() ::
  "-Q"  :: "utf8unchecked"     ::
  "-G"

fun p4CmdError(List{string} args)
  Error("p4 error (cmd: p4 " + args.string("", " ", "") + ")")

fun p4ParseError(List{string} args)
  Error("Failed to parse p4 response (cmd: p4 " + args.string("", " ", "") + ")")

// -- Parsers.

fun p4ErrorParser() -> Parser{string}
  pyMarshalParser(Type{P4RunOutput{string}}()).map(lambda (P4RunOutput{string} out)
    if out.code != P4RunCode.Error  -> Error("Not an error")
    else                            -> out.data
  )

fun p4ErrorMatcher(TextPattern expectedErr) -> Parser{string}
  p4ErrorParser().map(lambda (string err)
    if textMatch(expectedErr, err) -> err
    else                           -> Error("Unexpected error: '" + err.trim() + '\'')
  )

// -- Actions.

act p4Run(P4Config cfg, List{string} args) -> Either{string, Error}
  p = run("p4", cfg.p4GlobalArgs() :: args, ProcessFlags.PipeInOut | ProcessFlags.NewGroup);
  p.wait().map(impure lambda (ProcessResult r)
    if r.isSuccess() -> r.stdOut ?? ""
    else             ->
      p4ErrorParser().run(r.stdOut ?? "") as string err
        ? p4CmdError(args) :: Error(err.trim())
        : p4CmdError(args) :: Error((r.stdErr ?? "").trim())
  )

act p4Run{T}(P4Config cfg, List{string} args, Type{T} resultType) -> Either{T, Error}
  p = pyMarshalParser(resultType) ! p4ParseError(args);
  cfg.p4Run(args).map(p.build(ParseFlags.NoWrapperError))

act p4RunMany{T}(P4Config cfg, List{string} args, Type{T} resultType) -> Either{List{T}, Error}
  p = manyUntilParser(pyMarshalParser(resultType), endParser()) ! p4ParseError(args);
  cfg.p4Run(args).map(p.build(ParseFlags.NoWrapperError))

// -- Queries.

act p4RunGetVars(P4Config cfg) -> Either{List{P4Var}, Error}
  args = "set" :: "-q";
  p = manyParser(p4VarTxtParser(), newlineParser(), ParseFlags.AllowTrailingSeperator) ! p4ParseError(args);
  cfg.p4Run(args).map(p.build(ParseFlags.NoWrapperError))

act p4RunSetVar(P4Config cfg, P4Var var) -> Option{Error}
  cfg.p4Run("set" :: var.string()) as Error err ? err : None()

act p4RunCurrentStreamInfo(P4Config cfg) -> Either{P4StreamInfo, Error}
  cfg.p4Run("stream" :: "-o", Type{P4StreamInfo}())
  ! Error("No stream found for client '" + cfg.client + "' on server '" + cfg.server + '\'')

act p4RunClientInfo(P4Config cfg, P4Client client) -> Either{P4ClientInfo, Error}
  cfg.p4Run("client" :: "-o" :: client.id, Type{P4ClientInfo}())
  ! Error("Client '" + client + "' not valid on server '" + cfg.server + '\'')

act p4RunChangeInfo(P4Config cfg, P4Change change) -> Either{P4ChangeInfo, Error}
  cfg.p4Run("describe" :: "-s" :: change.string(), Type{P4ChangeInfo}())
  ! Error("No change '" + change.string() + "' found on server '" + cfg.server + '\'')

act p4RunChangeInfos(P4Config cfg, P4User u, int max, bool reverse) -> Either{List{P4ChangeInfo}, Error}
  cfg.p4RunMany(
    "changes"                         ::
    "-l"                              ::
    "-u" :: u.string()                ::
    (reverse ? Option("-r") : None()) ::
    "-m" :: max.string()              :: List{string}(),
    Type{P4ChangeInfo}())

act p4RunChangeInfos(P4Config cfg, P4User u, P4ChangeStatus s, int max, bool reverse) -> Either{List{P4ChangeInfo}, Error}
  cfg.p4RunMany(
    "changes"                         ::
    "-l"                              ::
    "-u" :: u.string()                ::
    "-s" :: s.string().strToLower()   ::
    (reverse ? Option("-r") : None()) ::
    "-m" :: max.string()              :: List{string}(),
    Type{P4ChangeInfo}())

act p4RunChangeInfos(P4Config cfg, P4Client c, P4ChangeStatus s, int max, bool reverse) -> Either{List{P4ChangeInfo}, Error}
  cfg.p4RunMany(
    "changes"                         ::
    "-l"                              ::
    "-c" :: c.string()                ::
    "-s" :: s.string().strToLower()   ::
    (reverse ? Option("-r") : None()) ::
    "-m" :: max.string()              :: List{string}(),
    Type{P4ChangeInfo}())

act p4RunVisibleChangeInfos(P4Config cfg, P4ChangeStatus s, int max, bool reverse) -> Either{List{P4ChangeInfo}, Error}
  cfg.p4RunMany(
    "changes"                         ::
    "-l"                              ::
    "-s" :: s.string().strToLower()   ::
    (reverse ? Option("-r") : None()) ::
    "-m" :: max.string()              ::
    ("//" + cfg.client.id + "/...")   :: List{string}(),
    Type{P4ChangeInfo}())

act p4RunGetDepotFiles(P4Config cfg, P4Change change, int max) -> Either{List{P4FileInfo}, Error}
  args            = "files" :: "-m" :: max.string() :: ("@=" + change.string());
  fileListParser  = manyUntilParser(pyMarshalParser(Type{P4FileInfo}()), endParser());
  noSuchFilesErr  = p4ErrorMatcher(GlobTextPattern("@=*no*file(s)*", StrComp.IgnoreCase));
  p               = (fileListParser | (noSuchFilesErr >> retParser(List{P4FileInfo}()))) ! p4ParseError(args);
  cfg.p4Run(args).map(p.build(ParseFlags.NoWrapperError))

act p4RunGetOpenFiles(P4Config cfg, P4Client client, P4Change change, int max) -> Either{List{P4OpenFile}, Error}
  cfg.p4RunMany(
    "opened"                  ::
    "-C"  :: client.string()  ::
    "-c"  :: change.string()  ::
    "-m"  :: max.string(),
     Type{P4OpenFile}())

act p4RunPendingResolves(P4Config cfg) -> Either{List{P4ResolveFile}, Error}
  args               = "resolve" :: "-n";
  fileListParser     = manyUntilParser(pyMarshalParser(Type{P4ResolveFile}()), endParser());
  noFileToResolveErr = p4ErrorMatcher(GlobTextPattern("No file(s) to resolve*", StrComp.IgnoreCase));
  p                  = (fileListParser | (noFileToResolveErr >> retParser(List{P4ResolveFile}()))) ! p4ParseError(args);
  cfg.p4Run(args).map(p.build(ParseFlags.NoWrapperError))
