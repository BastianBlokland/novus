import "std.ns"

import "../perforce/runner.ns"
import "../utils/job.ns"
import "../utils/style.ns"

// -- Utilities.

struct StatusData =
  P4Config            config,
  P4ClientInfo        clientInfo,
  P4StreamInfo        streamInfo,
  List{StatusResolve} pendingResolves,
  List{StatusChange}  pendingChanges,
  List{P4OpenFile}    defaultOpenFiles

struct StatusLimitedList{T} = List{T} files, int count, int max, bool truncated

struct StatusChange =
  P4ChangeInfo                    info,
  StatusLimitedList{P4OpenFile}   openFiles,
  StatusLimitedList{P4FileInfo}   shelvedFiles

struct StatusResolve =
  P4ClientFile    clientFile,
  P4ResolveType   resolveType

fun statusToLimitedList{T}(List{T} files, int max)
  count = files.length();
  truncated = count > max;
  StatusLimitedList{T}(files[0, max], min(count, max), max, truncated)

fun statusToResolve(P4ResolveFile resolve, P4ClientInfo clientInfo) -> Either{StatusResolve, Error}
  absPathToP4ClientFile(clientInfo, resolve.clientFile).map(lambda (P4ClientFile clientFile)
    StatusResolve(clientFile, resolve.resolveType)
  )

fun statusToResolves(List{P4ResolveFile} resolves, P4ClientInfo clientInfo) -> Either{List{StatusResolve}, Error}
  resolves.map(statusToResolve[clientInfo]).split().valueOrError()

act statusQueryChanges(P4Config cfg, StatusSettings settings) -> Either{List{StatusChange}, Error}
  p4RunChangeInfos(cfg, cfg.client, P4ChangeStatus.Pending, settings.changesMax).map(
      impure lambda (List{P4ChangeInfo} changes)
        changes.parallelMap(
          impure lambda (P4ChangeInfo info)
            p4RunGetOpenFiles(cfg, cfg.client, info.change, settings.filesMax + 1).map(
              impure lambda (List{P4OpenFile} openFiles)
                shelvedFiles = p4RunGetDepotFiles(cfg, info.change, settings.filesMax + 1) ?? List{P4FileInfo}();
                StatusChange(
                  info,
                  statusToLimitedList(openFiles, settings.filesMax),
                  statusToLimitedList(shelvedFiles, settings.filesMax))
            )
        ).split().valueOrError()
    )

act statusQuery(P4Config cfg, StatusSettings settings) -> Either{StatusData, Error}
  waitForJobs(Tuple(
    fork p4RunClientInfo(cfg, cfg.client),
    fork p4RunCurrentStreamInfo(cfg),
    fork p4RunPendingResolves(cfg),
    fork statusQueryChanges(cfg, settings),
    fork p4RunGetOpenFiles(cfg, cfg.client, p4DefaultChange(), settings.filesMax))
  ).map(impure lambda (Tuple{ P4ClientInfo,
                              P4StreamInfo,
                              List{P4ResolveFile},
                              List{StatusChange},
                              List{P4OpenFile}} res)
    statusToResolves(res.f3, res.f1).map(lambda (List{StatusResolve} resolves)
      StatusData(cfg, res.f1, res.f2, resolves, res.f4, res.f5))
    )

fun statusStyleForFileOp(P4FileOp op) -> Style
  if op == P4FileOp.Add         -> Style.OpAdd
  if op == P4FileOp.Edit        -> Style.OpEdit
  if op == P4FileOp.Delete      -> Style.OpDelete
  if op == P4FileOp.Integrate   -> Style.OpIntegrate
  if op == P4FileOp.Branch      -> Style.OpBranch
  if op == P4FileOp.MoveAdd     -> Style.OpMove
  if op == P4FileOp.MoveDelete  -> Style.OpMove
  else                          -> Style.Important

// -- Writers.

fun statusClientWriter(StyleCtx ctx) -> Writer{StatusData}
  (
    stringWriter() &
    litWriter(" (") & pathAbsWriter().styledWriter(ctx, Style.Detail) & litWriter(")")
  ).map(lambda (StatusData data)
    Tuple(data.clientInfo.client.string(), data.clientInfo.root))

fun statusStreamWriter(StyleCtx ctx) -> Writer{StatusData}
  (
    stringWriter() &
    litWriter(" (") & enumKeyWriter(Type{P4StreamType}()).styledWriter(ctx, Style.Detail) & litWriter(")") &
    ?(litWriter(" Parent: ").styledWriter(ctx, Style.Important) & stringWriter())
  ).map(lambda (StatusData data)
    Tuple(  data.streamInfo.stream.string(),
            data.streamInfo.type,
            data.streamInfo.parent.map(lambda (P4Stream s) s.string()))
  )

fun statusOpenFileWriter(StyleCtx ctx, int widthMax) -> Writer{P4OpenFile}
  (
    styleWriter(ctx) & enumKeyWriter(Type{P4FileOp}()) &
    litWriter(": ") & pathRelWriter() & styleResetWriter(ctx) &
    (litWriter(" #") & txtIntWriter()).styledWriter(ctx, Style.Detail)
  ).map(lambda (P4OpenFile c) Tuple(statusStyleForFileOp(c.Action), c.Action, c.clientFile.path, c.rev))

fun statusResolveFileWriter(StyleCtx ctx, int widthMax) -> Writer{StatusResolve}
  (
    (enumKeyWriter(Type{P4ResolveType}()) & litWriter(" resolve")).styledWriter(ctx, Style.Resolve) &
    litWriter(": ") & pathRelWriter().styledWriter(ctx, Style.Important)
  ).map(lambda (StatusResolve c) Tuple(c.resolveType, c.clientFile.path))

fun statusLimitedListCountWriter{T}() -> Writer{StatusLimitedList{T}}
  (
    txtIntWriter() & ?litWriter("+") & litWriter(" files")
  ).map(lambda (StatusLimitedList{T} l)
    Tuple(l.count, l.truncated)
  )

fun StatusLimitedListWriter{T}(Writer{T} w) -> Writer{StatusLimitedList{T}}
  (
    listWriter(newlineWriter() & indentWriter() & w) &
    ?(newlineWriter() & indentWriter() & litWriter("... (use '--files-max [COUNT]' to increase limit)"))
  ).map(lambda (StatusLimitedList{T} l) Tuple(l.files, l.truncated))

fun statusChangesWriter(StyleCtx ctx, int widthMax) -> Writer{StatusChange}
  (
    litWriter("[") &
    stringWriter().styledWriter(ctx, Style.Important) &
    litWriter("] ") &
    stringWriter() &
    (
      ?(
        newlineWriter() & indentWriter() & litWriter("Shelved: ") & statusLimitedListCountWriter{P4FileInfo}()
      ).styledWriter(ctx, Style.Important) &
      StatusLimitedListWriter(statusOpenFileWriter(ctx, widthMax))
    ).indent()
  ).map(lambda (StatusChange c)
    Tuple(
      c.info.change.string(),
      c.info.changeShortDesc(widthMax - 16),
      Tuple(c.shelvedFiles.count > 0 ? Option(c.shelvedFiles) : None(), c.openFiles))
  )

fun statusOutputWriter(StyleCtx ctx, int widthMax) -> Writer{StatusData}
  (
    litWriter("Client: ").styledWriter(ctx, Style.Important) & statusClientWriter(ctx) & newlineWriter() &
    litWriter("Stream: ").styledWriter(ctx, Style.Important) & statusStreamWriter(ctx) & newlineWriter() &
    litWriter("Resolves: ").styledWriter(ctx, Style.Important) & newlineWriter() &
      indentedListWriter(statusResolveFileWriter(ctx, widthMax)) &
    litWriter("Pending: ").styledWriter(ctx, Style.Important) & newlineWriter() &
      indentedListWriter(statusChangesWriter(ctx, widthMax)) &
    litWriter("Default: ").styledWriter(ctx, Style.Important) & newlineWriter() &
      indentedListWriter(statusOpenFileWriter(ctx, widthMax))
  ).map(lambda (StatusData data)
    Tuple(data, data, data.pendingResolves, data.pendingChanges, data.defaultOpenFiles)
  )

// -- Driver.

struct StatusSettings =
  bool        noColor,
  Option{int} widthMax,
  int         changesMax,
  int         filesMax

fun cliDefaults(Type{StatusSettings} t)
  CliDefault("changes-max",  "100") ::
  CliDefault("files-max",    "100")

act statusCmd(StatusSettings s) -> Option{Error}
  c         = consoleOpen().failOnError();
  widthMax  = s.widthMax ?? (c.termGetWidth() ?? 80);
  dataOrErr = loadP4Config().map(statusQuery[s]);
  writer    = statusOutputWriter(StyleCtx(!s.noColor && c.allowColor()), widthMax);
  if dataOrErr as Error      err  -> err
  if dataOrErr as StatusData data -> c.writeOut(writer, data)
