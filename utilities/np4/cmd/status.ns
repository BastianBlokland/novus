import "std.ns"

import "../perforce/runner.ns"
import "../utils/fork.ns"
import "../utils/limited-list.ns"
import "../utils/shared-writers.ns"
import "../utils/style.ns"

// -- Types.

struct StatusData =
  P4Config                config,
  P4ClientInfo            clientInfo,
  P4StreamInfo            streamInfo,
  List{StatusResolve}     pendingResolves,
  List{StatusChange}      pendingChanges,
  LimitedList{P4OpenFile} defaultOpenFiles

struct StatusChange =
  P4ChangeInfo            info,
  LimitedList{P4OpenFile} openFiles,
  LimitedList{P4FileInfo} shelvedFiles

struct StatusResolve =
  P4ClientFile    clientFile,
  P4ResolveType   resolveType

// -- Utilities.

fun statusFilterPending(P4ChangeInfo changeInfo, P4Config cfg) -> bool
  !p4ChangeIsHidden(cfg, changeInfo.change)

fun statusToResolve(P4ResolveFile resolve, P4ClientInfo clientInfo) -> Either{StatusResolve, Error}
  clientFileOrErr = absPathToP4ClientFile(clientInfo, resolve.clientFile);
  clientFileOrErr.map(lambda (P4ClientFile clientFile) StatusResolve(clientFile, resolve.resolveType))

fun statusToResolves(List{P4ResolveFile} resolves, P4ClientInfo clientInfo) -> Either{List{StatusResolve}, Error}
  resolves.map(statusToResolve[clientInfo]).split().valueOrError()

// -- Actions.

act statusQueryChange(P4ChangeInfo changeInfo, P4Config cfg, StatusSettings settings) -> Either{StatusChange, Error}
  waitForForks(Tuple(
    fork p4RunGetOpenFiles(cfg, cfg.client, changeInfo.change, settings.filesMax + 1),
    fork p4RunGetDepotFiles(cfg, changeInfo.change, settings.filesMax + 1))
  ).map(lambda (Tuple{List{P4OpenFile}, List{P4FileInfo}} res)
    StatusChange(
      changeInfo, toLimitedList(res.f1, settings.filesMax), toLimitedList(res.f2, settings.filesMax))
   )

act statusQueryChanges(P4Config cfg, StatusSettings settings) -> Either{List{StatusChange}, Error}
  changeInfosOrErr = p4RunChangeInfos(cfg, cfg.client, P4ChangeStatus.Pending, settings.changesMax, false);
  changeInfosOrErr.map(impure lambda (List{P4ChangeInfo} changes)
      changes
        .filter(statusFilterPending[cfg])
        .parallelMap(statusQueryChange[settings][cfg])
        .split()
        .valueOrError()
    )

act statusQuery(P4Config cfg, StatusSettings settings) -> Either{StatusData, Error}
  waitForForks(Tuple(
    fork p4RunClientInfo(cfg, cfg.client),
    fork p4RunCurrentStreamInfo(cfg),
    fork p4RunPendingResolves(cfg),
    fork statusQueryChanges(cfg, settings),
    fork p4RunGetOpenFiles(cfg, cfg.client, p4DefaultChange(), settings.filesMax + 1))
  ).map(impure lambda (Tuple{ P4ClientInfo,
                              P4StreamInfo,
                              List{P4ResolveFile},
                              List{StatusChange},
                              List{P4OpenFile}} res)
    statusToResolves(res.f3, res.f1).map(lambda (List{StatusResolve} resolves)
      StatusData(cfg, res.f1, res.f2, resolves, res.f4, toLimitedList(res.f5, settings.filesMax)))
    )

// -- Writers.

fun statusResolveFileWriter(StyleCtx ctx, StatusSettings s) -> Writer{StatusResolve}
  (
    resolveTypeWriter(ctx) & litWriter(' ') & pathRelWriter()
  ).map(lambda (StatusResolve c) Tuple(c.resolveType, c.clientFile.path))

fun statusChangeWriter(DateTime now, StyleCtx ctx, StatusSettings s) -> Writer{StatusChange}
  (
    changeInfoWriter(now, ctx, s.descLength, ChangeWriteFormat.Minimal) &
    ?(litWriter(' ') & shelveCountWriter(ctx).styledWriter(ctx, Style.Shelve)) &
    limitedListWriter(newlineWriter() & indentWriter() & openFileWriter(ctx)).indent()
  ).map(lambda (StatusChange c)
    Tuple(c.info, c.shelvedFiles.count > 0 ? Option(c.shelvedFiles) : None(), c.openFiles)
  )

fun statusOutputWriter(DateTime now, StyleCtx ctx, StatusSettings s) -> Writer{StatusData}
  (
    withHeaderWriter(ctx, "Client", clientInfoWriter(ctx)) & newlineWriter() &
    withHeaderWriter(ctx, "Stream", streamInfoWriter(ctx)) &
    ?(
      newlineWriter() & withHeaderWriter(ctx, "Resolves",
        listWriter(newlineWriter() & indentWriter() & statusResolveFileWriter(ctx, s)).indent())
    ) &
    ?(
      newlineWriter() & withHeaderWriter(ctx, "Pending",
        listWriter(newlineWriter() & indentWriter() & statusChangeWriter(now, ctx, s)).indent())
    ) &
    ?(
      newlineWriter() & withHeaderWriter(ctx, "Default",
        limitedListWriter(newlineWriter() & indentWriter() & openFileWriter(ctx)).indent())
    )
  ).map(lambda (StatusData d)
    Tuple(
      d.clientInfo,
      d.streamInfo,
      d.pendingResolves.noneIfEmpty(),
      d.pendingChanges.noneIfEmpty(),
      d.defaultOpenFiles.noneIfEmpty())
  )

// -- Driver.

struct StatusSettings =
  bool  noColor,
  int   descLength,
  int   changesMax,
  int   filesMax

fun cliDefaults(Type{StatusSettings} t)
  CliDefault("desc-length", "60")   ::
  CliDefault("changes-max", "50")   ::
  CliDefault("files-max",   "100")

act statusCmd(StatusSettings s) -> Option{Error}
  c         = consoleOpen().failOnError();
  styleCtx  = StyleCtx(!s.noColor && c.allowColor());
  writer    = statusOutputWriter(timeNow(), styleCtx, s);
  dataOrErr = loadP4Config().map(statusQuery[s]);
  if dataOrErr as Error      err  -> err
  if dataOrErr as StatusData data -> c.writeOut(writer & newlineWriter(), data)
