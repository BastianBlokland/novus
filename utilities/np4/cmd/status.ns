import "std.ns"

import "../perforce/runner.ns"
import "../utils/job.ns"
import "../utils/style.ns"

// -- Query.

struct StatusData =
  P4Config            config,
  P4ClientInfo        clientInfo,
  P4StreamName        streamName,
  List{P4ChangeInfo}  pendingChanges

act statusQuery(P4Config cfg, Console c) -> Either{StatusData, Error}
  waitForJobs(Tuple(
    fork p4RunClientInfo(cfg, cfg.client),
    fork p4RunCurrentStreamName(cfg),
    fork p4RunChangeInfos(cfg, cfg.user, cfg.client, P4ChangeStatus.Pending))
  ).map(impure lambda (Tuple{P4ClientInfo, P4StreamName, List{P4ChangeInfo}} res)
    StatusData(cfg, res.f1, res.f2, res.f3)
  )

// -- Writers.

fun statusStreamWriter(StyleCtx ctx) -> Writer{StatusData}
  (
    stringWriter().styleWriter(ctx, Style.Header) &
    litWriter(" (") &
    stringWriter().styleWriter(ctx, Style.Header) &
    litWriter(")")
  ).map(lambda (StatusData data) Tuple(data.clientInfo.stream.string(), data.streamName.string()))

fun statusChangesWriter(StyleCtx ctx, int maxWidth) -> Writer{P4ChangeInfo}
  (
    litWriter("[ ") &
    txtIntWriter(8).styleWriter(ctx, Style.Info) &
    litWriter(" ] ") &
    stringWriter()
  ).map(lambda (P4ChangeInfo c) Tuple(c.change.id, c.changeShortDesc(maxWidth - 16)))

fun statusOutputWriter(StyleCtx ctx, int maxWidth) -> Writer{StatusData}
  (
    litWriter("Stream: ") & statusStreamWriter(ctx) & newlineWriter() &
    litWriter("Pending: ") & newlineWriter() &
    indentedListWriter(statusChangesWriter(ctx, maxWidth))
  ).map(lambda (StatusData data) Tuple(data, data.pendingChanges))

// -- Driver.

struct StatusSettings =
  bool        noColor,
  Option{int} maxWidth

act statusCmd(StatusSettings s) -> Option{Error}
  c         = consoleOpen().failOnError();
  maxWidth  = s.maxWidth ?? (c.termGetWidth() ?? 80);
  dataOrErr = loadP4Config().map(statusQuery[c]);
  writer    = statusOutputWriter(StyleCtx(!s.noColor && c.allowColor()), maxWidth);
  if dataOrErr as Error      err  -> err
  if dataOrErr as StatusData data -> c.writeOut(writer, data)
