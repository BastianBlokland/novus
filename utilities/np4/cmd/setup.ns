import "std.ns"

import "../perforce/ignore.ns"
import "../perforce/runner.ns"
import "../utils/job.ns"
import "../utils/style.ns"

// -- Utilities.

struct NewWorkspace =
  P4Config      newCfg,
  P4ClientInfo  clientInfo

fun setupNewConfig(SetupSettings s)
  P4Config(s.server, s.client)

act setupSaveConfig(SetupSettings s, P4Config newCfg, P4ClientInfo clientInfo) -> Option{Error}
  res = writeP4Config(newCfg, clientInfo, s.force);
  res as Error err && err.code == PlatformError.FileAlreadyExists
    ? Error("Workspace already setup at: '" + clientInfo.root + "', use --force to overwrite")
    : res

act setupWorkspace(SetupSettings s, P4Config newCfg) -> Either{NewWorkspace, Error}
  p4RunClientInfo(newCfg, newCfg.client).map(impure lambda (P4ClientInfo clientInfo)
    waitForJobs(Tuple(
      fork setupSaveConfig(s, newCfg, clientInfo),
      fork p4AddToIgnore(newCfg, clientInfo, ".np4"))
    ).map(impure lambda (Tuple res) NewWorkspace(newCfg, clientInfo))
  )

// -- Writers

fun setupSuccessWriter(StyleCtx ctx) -> Writer{NewWorkspace}
  (
    litWriter("Workspace succesfully setup") & newlineWriter() &
    litWriter("Root: ") & pathAbsWriter().styleWriter(ctx, Style.Important) & newlineWriter() &
    litWriter("User: ") & stringWriter().styleWriter(ctx, Style.Important) & newlineWriter() &
    litWriter("Host: ") & stringWriter().styleWriter(ctx, Style.Important) & newlineWriter() &
    litWriter("Description: ") & stringWriter().styleWriter(ctx, Style.Important)
  ).map(lambda (NewWorkspace w)
    Tuple(
      w.clientInfo.root,
      w.clientInfo.owner.string(),
      w.clientInfo.host,
      w.clientInfo.description.trim())
    )

// -- Driver.

struct SetupSettings =
  bool      noColor,
  bool      force,
  P4Server  server,
  P4Client  client

act setupCmd(SetupSettings s) -> Option{Error}
  c               = consoleOpen().failOnError();
  style           = StyleCtx(!s.noColor && c.allowColor());
  newCfg          = setupNewConfig(s);
  workspaceOrErr  = setupWorkspace(s, newCfg);
  if workspaceOrErr as Error        err -> err
  if workspaceOrErr as NewWorkspace w   -> c.writeOut(setupSuccessWriter(style) & newlineWriter(), w)
