import "std.ns"

// -- Wait for a series of jobs (Either{T, Error}) to finish.
// Take a Tuple(future{Either{T, Error}}, ...) and turn it into a Either{Tuple(...), Error}.

fun waitForJobs{TupSrc, TupDst}(TupSrc s, Meta{#0} idx, TupDst result) -> Either{TupDst, Error}
  result

fun waitForJobs{TupSrc, Int, T, TupDst}(TupSrc s, Meta{Int} idx, Either{T, Error} job, TupDst result)
  if job as Error err -> err
  if job as T     val -> s.waitForJobs(idx, result.tupleAdd(val))

fun waitForJobs{TupSrc, Int, T, TupDst}(TupSrc s, Meta{Int} idx, Option{T} job, TupDst result)
  if job as Error err -> err
  else                -> s.waitForJobs(idx, result)

fun waitForJobs{TupSrc, Int, TupDst}(TupSrc s, Meta{Int} idx, TupDst result)
  resFuture = s.tupleGet(s.tupleSize() - idx);
  s.waitForJobs(--idx, resFuture.get(), result)

fun waitForJobs{TupSrc}(TupSrc s)
  s.waitForJobs(tupleSize(s), Tuple())

// -- Tests

assertEq(waitForJobs(Tuple()), Tuple())

assertEq(
  jobA = (lambda () -> Either{int, Error} 42);
  waitForJobs(Tuple(fork jobA())),
  Tuple(42)
)

assertEq(
  jobA = (lambda () -> Either{int, Error} 42);
  jobB = (lambda () -> Either{string, Error} "Hello World");
  waitForJobs(Tuple(fork jobA(), fork jobB())),
  Tuple(42, "Hello World")
)

assertEq(
  jobA = (lambda () -> Either{int, Error} 42);
  jobB = (lambda () -> Either{string, Error} "Hello World");
  jobC = (lambda () -> Either{float, Error} 1.337);
  waitForJobs(Tuple(fork jobA(), fork jobB(), fork jobC())),
  Tuple(42, "Hello World", 1.337)
)

assertEq(
  jobA = (lambda () -> Either{int, Error} Error("Fail"));
  jobB = (lambda () -> Either{string, Error} "Hello World");
  waitForJobs(Tuple(fork jobA(), fork jobB())),
  Error("Fail")
)

assertEq(
  jobA = (lambda () -> Either{int, Error} 42);
  jobB = (lambda () -> Either{string, Error} Error("Fail"));
  waitForJobs(Tuple(fork jobA(), fork jobB())),
  Error("Fail")
)
