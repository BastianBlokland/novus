import "database.ns"

enum CmdWriteFlags =
  None            : 0b00,
  QuotePath       : 0b01,
  DoubleQuotePath : 0b10

fun cmdQuoteWriter(CmdWriteFlags flags) -> Writer{None}
  singleQuote = (flags & CmdWriteFlags.QuotePath) != 0;
  doubleQuote = (flags & CmdWriteFlags.DoubleQuotePath) != 0;
  (singleQuote || doubleQuote) ? litWriter(doubleQuote ? '"' : '\'') : noneWriter()

fun cmdQuotedWriter{T}(Writer{T} writer, CmdWriteFlags flags) -> Writer{T}
  cmdQuoteWriter(flags) & writer & cmdQuoteWriter(flags)

fun cmdWriter(CmdWriteFlags flags = CmdWriteFlags.None) -> Writer{Cmd}
  (
    cmdQuotedWriter(pathAbsWriter(), flags) & ?(litWriter(' ') & listWriter(stringWriter(), litWriter(' ')))
  ).map(lambda (Cmd c) Tuple(c.path, !c.args.isEmpty() ? c.args : None()))

fun entryTimeWriter() -> Writer{Entry}
  timeWriter = dateTimePrettyWriter(PrettyTimeFlags.None);
  (
    padUntilWriter(20) & litWriter("Creation ") & padUntilWriter(30) & timeWriter & newlineWriter() &
    padUntilWriter(20) & litWriter("Access ")   & padUntilWriter(30) & timeWriter
  ).map(lambda (Entry e) Tuple(e.creationTime, e.accessTime))

fun entryWriter(bool includeTime, int padKeyUntilCol = 15, int maxCol = 80) -> Writer{Entry}
  (
    stringWriter().padLeftWriter(padKeyUntilCol) & litWriter(" -> ") &
    splitWriter(cmdWriter(), wrapWriter(stringWriter(), maxCol, newlineWriter() & padUntilWriter(padKeyUntilCol + 4))) &
    ?(newlineWriter() & entryTimeWriter())
  ).map(lambda (Entry e) Tuple(e.key, e.value, includeTime ? e : None()))
