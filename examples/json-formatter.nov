// --- Example of a small application to format json files

import "std.nov"

act formatJson(Path path, bool pretty)
  parseRes = path.fileRead(jsonParser());
  if parseRes as JsonValue v  -> printJson(v, pretty)
  if parseRes as Error err    -> print("Unable to parse json: " + err)

act printJson(JsonValue v, bool pretty)
  indent      = pretty ? "  " : "";
  newlineMode = pretty ? WriterNewlineMode.Lf : WriterNewlineMode.None;
  str = v.string(indent, newlineMode);
  print(str)

act printUsage()
  print("Json formatter");
  print("Usage: json-formatter.nov --path path/to/file.json --pretty\n");
  print("Options:");
  print("  --help     Print this usage information");
  print("  --path     Path to the json file to format");
  print("  --pretty   Should the output be pretty formatted (multiple lines)")

act main()
  if hasEnvOpt("help") -> printUsage()
  else ->
    pathOrErr = getEnvOpt("path", pathParser());
    pretty    = hasEnvOpt("pretty");
    if pathOrErr as Path path -> formatJson(path, pretty)
    else                      -> printErr("'path' option is required"); fail()

main()
