// --- Example of a small application to format json files

import "std.ns"

act formatJson(Path path, bool pretty)
  json = path.fileRead(jsonParser()).failOnError();
  printJson(json, pretty)

act printJson(JsonValue v, bool pretty)
  indent      = pretty ? "  " : "";
  newlineMode = pretty ? WriterNewlineMode.Lf : WriterNewlineMode.None;
  str = v.string(indent, newlineMode);
  print(str)

act printUsage()
  print("Json formatter");
  print("Usage: json-formatter.ns --path path/to/file.json --pretty\n");
  print("Options:");
  print("  --help     Print this usage information");
  print("  --path     Path to the json file to format");
  print("  --pretty   Should the output be pretty formatted (multiple lines)")

act main()
  if hasEnvOpt("help") -> printUsage()
  else ->
    path    = getEnvOpt("path", pathParser()).failOnError();
    pretty  = hasEnvOpt("pretty");
    formatJson(path, pretty)

main()
